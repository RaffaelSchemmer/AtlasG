unset title
unset label
set autoscale
set title "Potência média nos roteadores de uma NoC com 64 bits e buffer depth=16"
set xlabel "% Taxa Total"
set ylabel "Potência Média (mW)"
#-------------------------------------------------------------------------------
# Inport Equations
#-------------------------------------------------------------------------------
a_inport=0.00292561
b_inport=55.8978
inport(x)=(a_inport*x)+b_inport
#-------------------------------------------------------------------------------
# Swicth Control Equations
#-------------------------------------------------------------------------------
a_swctrl=0.000148313
b_swctrl=2.87516
swctrl(x)=(a_swctrl*x)+b_swctrl
#-------------------------------------------------------------------------------
# Outport Equations
#-------------------------------------------------------------------------------
a_ouport=0.000463615
b_ouport=0.0592622
ouport(x)=(a_ouport*x)+b_ouport
#-------------------------------------------------------------------------------
# Router Equations
#-------------------------------------------------------------------------------
routerBL(x)=3*inport(x)+ swctrl(x) + 3*ouport(x)
routerBC(x)=4*inport(x)+ swctrl(x) + 4*ouport(x)
routerCC(x)=5*inport(x)+ swctrl(x) + 5*ouport(x)
set xrange [0:50]
plot routerBL(x) title 'RouterBL, RouterBR, RouterTL, RouterTR', \
     routerBC(x) title 'RouterBC, RouterCL, RouterCR, RouterTC', \
     routerCC(x) title 'RouterCC'
pause -1 "Press ENTER to continue"	 
